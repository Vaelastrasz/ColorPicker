namespace colorPickerFeature {

  
  var ui_colorPopup;
  var ui_preview;
  var ui_paliter;
  var ui_resetPreview;


  function self.ready() {
    ui_preview = $(.previewColor);
    ui_paliter = $(.colorsPaliter);
    ui_colorPopup = $(.selectColorPopup);
    ui_resetPreview = $(.resetColor);
    stdout.println("PREVIEW ATTACHED");
  }

  class Preview : Element {
    function attached() {
      self.on("colorChanged", function(evt){ui_preview.style#background-color = evt.data.color;});
      self.on("resetPreview", function(evt){ui_preview.style#background-color = ui_resetPreview.style#background-color;});
    }
  }

  class PersistAbstractModel {
    var m_currentItem;
    var m_items = [];
    
    function setCurrentModelItem(item) {
      m_currentItem = item;
    }

    function getCurrentModelItem() {
      return m_currentItem;
    }

    function setItemsArray(items) {
      m_items = items;
    }

    function getItemsArray() {
      return m_items;
    }
  }

  class PersistData : PersistAbstractModel {

    function serializeToJSON() {
      const outputStream = Stream.openFile("D:\\Shared\\Projects\\ColorPicker\\resources\\data.clr", "w");
      
      const current = JSON.stringify(PersistAbstractModel.getCurrentModelItem());
      outputStream.println(current); 
      
      const array = JSON.stringify(PersistAbstractModel.getItemsArray());
      outputStream.println(array);
      
      outputStream.close();
    }

    function deserializeJSON() {

      const inputStream = Stream.openFile("D:\\Shared\\Projects\\ColorPicker\\resources\\data.clr", "r");
      
      var currentRead = inputStream.readln();
      PersistAbstractModel.setCurrentModelItem(JSON.parse(currentRead));
      
      var blocksRead = inputStream.readln();
      PersistAbstractModel.setItemsArray(JSON.parse(blocksRead));
      
      GeneralController.updateUi(false);
      inputStream.close();
    }  

    function getBlockByID(searchID) {
      var blockToSearch = PersistAbstractModel.getItemsArray().find(:block: block.id == searchID);
      return blockToSearch;
    }

    function getIndexByID(searchID) {
      return PersistAbstractModel.getItemsArray().indexOf(getBlockByID(searchID));
    }

  }

  class GeneralController {
    
    var persist = new PersistData();  

    function resetPreview() {
      self.postEvent("resetPreview");
    }

    function setCurrentBlock(block) {
      persist.setCurrentModelItem(block);
      self.postEvent("colorChanged", {color: block.color});
    }

    function getBlockByDOM(DOMElement) {
      return persist.getBlockByID(DOMElement.attributes["blockID"]);
    }

    function generateId() {
      var id = "id" + Math.random().toString(16).slice(2);
      stdout.println(id);
      while (persist.getBlockByID(id)) { 
        id = "id" + Math.random().toString(16).slice(2); //unique ID fix
      } 
      return id;
    }

    function generateBlock(_color) {
      var blockElement = {
        id: generateId();
        color: _color;
      }
      stdout.println("Element generated! Id: " + blockElement.id);
      return blockElement;
    }

    function editBlock(id, newColor) {
      var blockToEdit = persist.getBlockByID(id);
      blockToEdit.color = newColor;
      setCurrentBlock(blockToEdit);
    }
    
    function deleteBlock(id) {
      var indexToDelete = persist.getIndexByID(id);
      persist.getItemsArray().splice(indexToDelete, 1);
      resetPreview();
      if (persist.getItemsArray().length > 0) {
        setCurrentBlock(persist.getItemsArray()[persist.getItemsArray().length - 1]);
      } else {
        persist.setCurrentModelItem(undefined);
      }
    }
  
    function updateUi(isDeleteOldData = true) {
      if (removeAllBlocks(isDeleteOldData) && addColorsToUI()) {
        stdout.println("Updating UI success!");
        return true;  
      }
      stdout.println("Updating UI error occured!");
      return false;
    }
    
    function addNewBlock(color) {
      var sampleBlock = "<div class=\"colorBlock colorControl\" />";
      var generatedBlock = generateBlock(color);

      ui_paliter.append(sampleBlock);
      ui_paliter[ui_paliter.length - 1].style#background-color = color;
      ui_paliter[ui_paliter.length - 1].attributes["blockID"] = generatedBlock.id;
      
      persist.getItemsArray().push(generatedBlock);
      stdout.println("Added block " + generatedBlock.id + " \nNew blockArray: " + persist.getItemsArray());
      setCurrentBlock(generatedBlock);
    }
    
    function removeAllBlocks(isDeleteOldData = true) {
      var availableBlocks = $$(.colorControl);
      for (var i = 0; i < availableBlocks.length; i++) {
        availableBlocks[i].remove(); 
      }

      if (isDeleteOldData) {
        persist.setItemsArray([]);
      }

      resetPreview();
      return true;
    }

    function addColorsToUI() {
      for (var block in persist.getItemsArray()) {
        stdout.println(block);
        var blockToInsert = "<div class=\"colorBlock colorControl\" />";
        ui_paliter.append(blockToInsert);
        ui_paliter[ui_paliter.length - 1].attributes["blockID"] = block.id;
        ui_paliter[ui_paliter.length - 1].style#background-color = block.color; 
      }
      setCurrentBlock(persist.getCurrentModelItem()? persist.getCurrentModelItem() : 
                                              getBlockByDOM(ui_paliter[ui_paliter.length - 1]));

      return true;
    }

    function setBlockArray(colorArray) {
      removeAllBlocks();
      persist.setItemsArray(colorArray);
      addColorsToUI();
    }
  }

  class ColorPicker: Element {
    this var editBtn;
    this var deleteBtn;

    function attached() {
      this.editBtn = $(.editColor);
      this.deleteBtn = $(.deleteColor);
      this.onClick = onClick;
      // this.editBtn.onClick = editClick.bind(this);
      // this.deleteBtn.onClick = deleteClick.bind(this);
      this.editBtn.onClick = editClick;
      this.deleteBtn.onClick = deleteClick;
    }
    
    function onClick() {
      ui_colorPopup.attributes["type"] = "edit";
      this.popup(ui_colorPopup, 2);
      GeneralController.setCurrentBlock(GeneralController.getBlockByDOM(this));
    }

    function editClick() {
      var newColor = $(widget#color).value.toString();
      GeneralController.editBlock(this.parent.owner.attributes["blockID"], newColor);
      this.parent.owner.style#background-color = color(newColor);
    }

    function deleteClick() {
      GeneralController.deleteBlock(this.parent.owner.attributes["blockID"]);
      this.parent.owner.remove();
    }
  }

  class ResetColor: Element {
    function attached() {
      this.onClick = onClick;
    }
    function onClick() {
      // GeneralController.resetPreview();
      GeneralController.persist.serializeToJSON();
      GeneralController.persist.deserializeJSON();
    }
  }

  class SelectColor: Element {
    var addBtn;

    function attached() {
      this.onClick = onClick;
      addBtn = $(.addColor);
      addBtn.onClick = addClick;
    }

    function onClick() {
      ui_colorPopup.attributes["type"] = "add";
      this.popup(ui_colorPopup, 2);
    }

    function addClick() {
      var chosenColor = $(widget#color).value.toString();
      GeneralController.addNewBlock(chosenColor);      
      ui_colorPopup.closePopup();
    }
  }

  class ColorsPaliter: Element {

    const maxElementWidth = 550;
    function attached() {
      stdout.println("ColorsPaliter Attached");
      view.on("size", function() {
        ui_paliter.attributes["type"]  = (ui_paliter.box(#width) < maxElementWidth ? "mesh" : "row");
      });
    }
  }
}